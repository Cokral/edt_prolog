
\subsection{Algorithme}

\subsubsection{Résolution}

\begin{lstlisting}[language=Prolog, caption=Resolution, captionpos=b,
label={lst:reso}]
/**
 * planifier(+Ss, -Cs).
 *
 * @arg S   Listes des seances a planifier
 * @arg C   Listes des creneaux construits
 */
planifier([], []) :- !.
planifier(Ss, [C|Cs]) :-

    member(S, Ss),      % La seance courante
    delete(Ss, S, Ss2), % On l'enleve de la liste

    planifier(Ss2, Cs), % on traite le sous-probleme

    % Creation du creneau et tests ---------------------------------------------

    seance(S, TypeS, _, _),

    date(J, M),     % une date
    plage(H, _, _), % une plage horaire

    % une salle
    salle(L, TailleL),
    accueille(L, TypeL),
    typesCoursIdentiques(TypeS, TypeL), % type de salle valide

    findall(G, groupeSeance(G, S), Gs), % tous les groupes de la seance
    effectifGroupes(Gs, Effectif),
    Effectif =< TailleL, % taille de salle valide

    findall(P, profSeance(P, S), Ps),   % tous les enseignants de la seance

    % test des contraintes (profs, incompatibilite groupes, sequencement)
    % sur cette proposition de creneau
    creneauValide(S, Ps, Gs, H, J, M, L, Cs),

    % Fin creation du creneau et tests -----------------------------------------

    C = [S, H, J, M, L].
\end{lstlisting}

Voici dans le code \ref{lst:reso} ci-dessus l'algorithme principal \texttt{planifier(Ss,
Cs)}.
L'idée est de découper le problème en sous-problèmes par récursion. La
planification d'une séance demande ainsi la planification des séances la
précédant dans la liste \texttt{Ss} initiale (sous-problème).

La planification d'une séance consiste donc à faire les choix non déterministes
présentés dans l'algorithme \ref{algo:algo1}. L'ordre d'éxécution de ces choix
a son importance : le back-tracking de Prolog remontera sur les choix pour
trouver une combinaison validant les conditions dans l'ordre que nous avons
défini.

Pour chaque séance, on détermine donc tout d'abord le moment, puis la salle,
avant de tester si le créneau formé est valide. Si ce n'est pas le cas, Prolog
testera toutes les autres salles avant de rechercher une autre date.
Choisir l'ordre inverse (choix de la salle avant la date) donnerait un emploi du
temps s'étalant sur une période plus long, Prolog préférant alors passer à un
moment suivant plutôt que de cherche une salle libre (ie. peu de cours en
parallèle).

Exemple : le 01/01, de 14h à 15h30, on peut avoir plusieurs séances avec des
groupes différents et des salles différentes.

À chaque propositon de créneau, on effectue les tests de validité des
posts-conditions :

\begin{itemize}

    \item On vérifie que la salle le bon type de cours.

    \item On se penche ensuite sur les problèmes de groupes. On récupère les
        groupes associés à la séance, et on vérifie que la salle convient à
        l'effectif .

    \item Les autres post-conditions nécessitent de parcourir les créneaux déjà
        construits. C'est \texttt{creneauValide} qui s'en charge. On l'explique
        plus bas.

\end{itemize}

Il est donc important d'effectuer les tests au plus tôt après le choix non
déterministe. On optimise ainsi les calculs nécessaire. Prolog ne lancera par
exemple pas le \texttt{findall} des groupes tant qu'une salle de bon type n'est
pas trouvée.

\subsubsection{Prédicats utilitaires}

Des prédicats déterministes ont été implémentés afin de participer à la
validation des post-conditions, à l'ajout d'un créneau potentiel.

\begin{lstlisting}[language=Prolog, caption=creneauValide, captionpos=b,
label={lst:creneauValide}]
/**
 * creneauValide(+S, +Ps, +Gs, +H, +J, +M, +L, +Cs).
 *
 * Definit si un creneau est valide (Pas de conflit avec les creneaux existants)
 *
 * @arg S   La seance
 * @arg Ps  Les enseignants
 * @arg Gs  Les groupes
 * @arg H   La plage horaire
 * @arg J   Le jour
 * @arg M   Le mois
 * @arg L   La salle
 * @arg Cs  Liste de creneaux [S, H, J, M, L]
 */
creneauValide(_, _, _, _, _, _, _, []) :- !.
creneauValide(S, Ps, Gs, H, J, M, L, [C|Cs]) :-
    creneauValideCreneau(S, Ps, Gs, H, J, M, L, C),
    creneauValide(S, Ps, Gs, H, J, M, L, Cs),
    !.
\end{lstlisting}

Le code du listing \ref{lst:creneauValide} montre la logique récursive de la
validation d'une nouveau créneau.

\begin{lstlisting}[language=Prolog, caption=creneauValide, captionpos=b,
label={lst:creneauValideCreneau}]
/**
 * creneauValideCreneau(+S, +Ps, +Gs, +H, +J, +M, +L, +C).
 *
 * Definit si un cours n'est pas incompatible au moment donne avec les autres
 * cours de la liste de creneaux.
 *
 * @arg Ps  Les enseignants
 * @arg Gs  Les groupes
 * @arg H   La plage horaire
 * @arg J   Le jour
 * @arg M   Le mois
 * @arg L   La salle
 * @arg C   Un creneau [S, H, J, M, L]
 */
creneauValideCreneau(S, Ps, Gs, H, J, M, L, C) :-
    % le creneau valide le sequencement avec C
    sequencementValideCreneau(S, H, J, M, C),
    (
        % le creneau n'est pas au meme moment que C
        (\+ memeMomentCreneau(H, J, M, C));
        % ou il ne concerne pas un meme prof, des groupes incompatibles
        % ou une meme salle
        (
            \+ groupesIncompatibleCreneau(Gs, C),
            \+ memeProfs(Ps, C),
            \+ memeSalle(L, C)
        )
    ),
    !.
\end{lstlisting}

Le code du listing \ref{lst:creneauValideCreneau} montre les tests effectués
face à chaque créneau. Les autres prédicats déterministes sont documentés dans
\texttt{planifier.pl}.

\begin{itemize}

    \item \texttt{sequencementValideCreneau(+S, +H, +J, +M, +C)} vérifie que
        le nouveau créneau respecte les contraintes de séquencement avec le
        créneau \texttt{C} existant.

    \item \texttt{\textbackslash+ memeMomentCreneau(+H, +J, +M, +C)} teste si
        les deux créneaux ont lieu en même temps.

    \item \texttt{\textbackslash+ groupesIncompatibleCreneau(+Gs, +C)} vérifie
        que les groupes ne sont pas incompatibles avec les groupes du créneau C.

    \item \texttt{\textbackslash+ memeProfs(+Ps, +C)} vérifie que tous les profs
        du créneau sont disponibles.

    \item \texttt{\textbackslash+ memeSalle(+L, +C)} vérifie que la salle est
        libre.

\end{itemize}



